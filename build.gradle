plugins {
  id 'java'
  id 'osgi'
  id 'com.jfrog.bintray' version '1.8.0'
  id 'maven-publish'
  id 'net.researchgate.release' version '2.6.0'
  id 'com.github.ben-manes.versions' version '0.17.0'
  id 'ru.vyarus.animalsniffer' version '1.5.0'
}

repositories {
  jcenter()
}

group = 'com.palominolabs.metrics'

ext.deps = [
    metrics: '4.0.2',
    slf4j  : '1.7.25',
    guice  : '4.1.0'
]
ext.isReleaseVersion = !version.endsWith('SNAPSHOT')

dependencies {
  compile "io.dropwizard.metrics:metrics-core:$deps.metrics"
  compile "io.dropwizard.metrics:metrics-annotation:$deps.metrics"
  compile "com.google.inject:guice:$deps.guice"
  compile 'com.google.code.findbugs:jsr305:3.0.2'

  testRuntime "org.slf4j:slf4j-simple:$deps.slf4j"
  testRuntime "org.slf4j:jul-to-slf4j:$deps.slf4j"
  testRuntime "org.slf4j:log4j-over-slf4j:$deps.slf4j"
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-all:1.3'

  signature 'org.codehaus.mojo.signature:java16:1.1@signature'
}

sourceCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
  options.compilerArgs << '-Xlint:unchecked'
  options.deprecation = true
}

task sourceJar(type: Jar, dependsOn: classes) {
  from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  from javadoc.destinationDir
}

def pomConfig = {
  licenses {
    license {
      name "COIL 0.4"
      url "https://github.com/palominolabs/metrics-guice/blob/master/LICENSE.txt"
      distribution "repo"
    }
  }
  developers {
    developer {
      id "mpierce"
      name "Marshall Pierce"
      email "github@contact.mpierce.org"
    }
  }

  scm {
    url "https://github.com/palominolabs/metrics-guice"
  }
}

publishing {
  publications {
    bintray(MavenPublication) {
      from components.java
      groupId project.group
      artifactId project.name
      version project.version

      artifact sourceJar {
        classifier 'sources'
      }

      artifact javadocJar {
        classifier 'javadoc'
      }

      pom.withXml {
        def root = asNode()
        root.appendNode('description', 'Metrics integration with Guice')
        root.appendNode('name', 'metrics-guice')
        root.appendNode('url', 'https://github.com/palominolabs/metrics-guice')
        root.children().last() + pomConfig
      }
    }
  }
}

bintray {
  user = rootProject.hasProperty('bintrayUser') ? rootProject.property('bintrayUser') : 'FIXME'
  key = rootProject.hasProperty('bintrayApiKey') ? rootProject.property('bintrayApiKey') : 'FIXME'
  publications = ['bintray']

  pkg {
    repo = 'maven'
    licenses = ['Copyfree']
    vcsUrl = 'https://github.com/palominolabs/metrics-guice'
    name = 'com.palominolabs.metrics:metrics-guice'

    version {
      name = project.version
      released = new Date()
      if (project.isReleaseVersion) {
        vcsTag = 'v' + project.version
      }
    }
  }
}

release {
  tagTemplate = 'v$version'
}
afterReleaseBuild.dependsOn bintrayUpload
